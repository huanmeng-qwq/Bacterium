plugins {
    // see https://fabricmc.net/develop/ for new versions
    id 'fabric-loom' version '1.8-SNAPSHOT' apply false
    // see https://projects.neoforged.net/neoforged/moddevgradle for new versions
    id 'net.neoforged.moddev' version '2.0.49-beta' apply false

    id "me.modmuss50.mod-publish-plugin" version "0.8.4" apply false
}

subprojects {
    if (name !in ["fabric", "neoforge", "forge"]) {
        return
    }
    afterEvaluate {
        publishMods {
            changelog = "Update version ${project.version}"
            type = STABLE
            displayName = "[${project.name.replace(':', '')} $minecraft_version] v${project.version}"
            curseforge {
                projectId = "412035"
                accessToken = providers.environmentVariable("CURSEFORGE_API_KEY")
                clientRequired = true
                serverRequired = true
                javaVersions.add(JavaVersion.VERSION_21)
                minecraftVersions.add(minecraft_version)
            }
            modrinth {
                projectId = "CtYK1BTD"
                accessToken = providers.environmentVariable("MODRINTH_TOKEN")
                minecraftVersions.add(minecraft_version)
            }
            github {
                accessToken = providers.environmentVariable("GITHUB_TOKEN")
                repository = "huanmeng-qwq/Bacterium"
                commitish = "master"
                tagName = "release/$minecraft_version-${project.version}"
            }
        }
    }
}